/*
 * ProxyChat, a Velocity chat solution
 * Copyright (C) 2020 James Lyne
 *
 * Based on BungeeChat2 (https://github.com/AuraDevelopmentTeam/BungeeChat2)
 * Copyright (C) 2020 Aura Development Team
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.1.0-m3'
}

rootProject.ext.mavenUserName=""
rootProject.ext.mavenPassword=""

if (file("./build.gradle.local").exists()) {
    apply from: "./build.gradle.local"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://papermc.io/repo/repository/maven-public/" }
        maven { url 'https://repo.not-null.co.uk/releases/' }
        maven { url 'https://repo.not-null.co.uk/snapshots/' }
        maven { url 'https://repo.lucko.me/' }
        maven { url 'https://jitpack.io' }
        flatDir {
            dirs 'libs'
        }
    }

    group = 'uk.co.notnull'
    version = '1.1-SNAPSHOT'
    description = 'Velocity Discord integration solution'

    java.sourceCompatibility = JavaVersion.VERSION_17
    java.targetCompatibility = JavaVersion.VERSION_17

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }
}

dependencies {
    implementation project(':ProxyChatApi')
    implementation name: 'MessagesTranslator-1.4.3.41-DEV'
    implementation 'com.typesafe:config:1.4.2'

    compileOnly 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'uk.co.notnull:PlatformDetection-velocity:2.0'
    compileOnly 'uk.co.notnull:ProxyDiscordAPI:1.1-SNAPSHOT'

    annotationProcessor 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'

    testImplementation project(':ProxyChatApi')
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'ch.vorburger.mariaDB4j:mariaDB4j:2.5.3'
    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation name: 'MessagesTranslator-1.4.3.41-DEV'
    testImplementation 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
    testImplementation "org.mockito:mockito-core:3.6.0"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.9"
    testImplementation "org.powermock:powermock-module-junit4:2.0.9"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:4.2.0"

    testCompileOnly 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'

    testRuntimeOnly 'org.mariadb.jdbc:mariadb-java-client:2.7.3'
    testRuntimeOnly 'org.slf4j:slf4j-nop:1.7.36'
}

description = 'A velocity chat solution'

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    build {
        dependsOn(shadowJar)
    }

    shadowJar {
        archiveClassifier.set('')
    }
}
